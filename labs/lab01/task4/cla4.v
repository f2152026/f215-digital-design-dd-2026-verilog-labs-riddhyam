// cla4.v

module cla4(

  input  [3:0] a,
  input  [3:0] b,
  input        cin,
  output [3:0] sum,
  output       cout

);

  wire p0, p1, p2, p3;
  wire g0, g1, g2, g3;

  wire c1, c2, c3, c4;

  wire c1_t0;
  wire c2_t0, c2_t1;
  wire c3_t0, c3_t1, c3_t2;
  wire c4_t0, c4_t1, c4_t2, c4_t3;

  // Generate and Propagate

  xor #(2) (p0, a[0], b[0]);
  xor #(2) (p1, a[1], b[1]);
  xor #(2) (p2, a[2], b[2]);
  xor #(2) (p3, a[3], b[3]);

  and #(2) (g0, a[0], b[0]);
  and #(2) (g1, a[1], b[1]);
  and #(2) (g2, a[2], b[2]);
  and #(2) (g3, a[3], b[3]);

  // c1 = g0 + p0.cin

  and #(2) (c1_t0, p0, cin);
  or  #(2) (c1, g0, c1_t0);

  // c2 = g1 + p1.g0 + p1.p0.cin

  and #(2) (c2_t0, p1, g0);
  and #(2) (c2_t1, p1, p0, cin);
  or  #(2) (c2, g1, c2_t0, c2_t1);

  // c3 = g2 + p2.g1 + p2.p1.g0 + p2.p1.p0.cin

  and #(2) (c3_t0, p2, g1);
  and #(2) (c3_t1, p2, p1, g0);
  and #(2) (c3_t2, p2, p1, p0, cin);
  or  #(2) (c3, g2, c3_t0, c3_t1, c3_t2);

  // c4 = g3 + p3.g2 + p3.p2.g1
  //      + p3.p2.p1.g0 + p3.p2.p1.p0.cin

  and #(2) (c4_t0, p3, g2);
  and #(2) (c4_t1, p3, p2, g1);
  and #(2) (c4_t2, p3, p2, p1, g0);
  and #(2) (c4_t3, p3, p2, p1, p0, cin);

  or #(2) (c4, g3, c4_t0, c4_t1, c4_t2, c4_t3);

  // Carry out

  assign #(2) cout = c4;

  // Sum

  xor #(2) (sum[0], p0, cin);
  xor #(2) (sum[1], p1, c1);
  xor #(2) (sum[2], p2, c2);
  xor #(2) (sum[3], p3, c3);

endmodule
